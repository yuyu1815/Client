default:
    @just --list

launcher-dev *args:
    @mise exec -- pnpm --filter pomme-launcher tauri dev {{ args }}

launcher-build *args:
    @pnpm --filter pomme-launcher tauri build {{ args }}

launcher-pre-pr:
    @cargo fmt -p pomme-launcher -- --check
    @cargo clippy -p pomme-launcher --release --all-targets --all-features -- -D warnings
    @pnpm --filter pomme-launcher pre-pr

# Stage Minecraft's OpenAL Soft next to the dev binaries, as the release job does.
openal:
    #!/usr/bin/env bash
    python=$(command -v python3 || command -v python) || {
        echo "warning: no python on PATH, skipping OpenAL staging (audio will be disabled)" >&2
        exit 0
    }
    target_dir="${CARGO_TARGET_DIR:-target}"
    "$python" tools/fetch_openal.py "$target_dir/debug" "$target_dir/dev-fast" "$target_dir/release"

client-dev *args: openal
    @cargo run -p pomme-client {{ args }}

# Optimized release client for accurate benchmarking (supplies the launch token the guard needs).
client-release *args: openal
    #!/usr/bin/env bash
    cargo run --release -p pomme-client -- --launch-token "$(mktemp)" {{ args }}

client-build *args: openal
    @cargo build -p pomme-client {{ args }}

# TODO: CI also runs the client's clippy and tests with --no-default-features; mirror it here.

client-pre-pr:
    @cargo fmt -p pomme-client -- --check
    @cargo fmt -p pomme-protocol -- --check
    @cargo fmt -p pomme-singleplayer -- --check
    @cargo clippy -p pomme-client --release --all-targets --all-features -- -D warnings
    @cargo clippy -p pomme-protocol --release --all-targets --all-features -- -D warnings
    @cargo clippy -p pomme-singleplayer --release --all-targets -- -D warnings
    @cargo test -p pomme-protocol
    @cargo test -p pomme-client
    @cargo test -p pomme-singleplayer -- --include-ignored

# Regenerate a version's packet-id table from the decompiled reference.
protogen version="26.2":
    @cargo run -p protogen -- reference/{{ version }}/decompiled {{ version }} pomme-protocol/src/data/protocol-{{ version }}.json

# Regenerate a version's client-registry id table from the data-generator report.
registrygen version="26.2":
    @cargo run -p protogen -- registries reference/{{ version }} {{ version }} pomme-protocol/src/data/registries-{{ version }}.json

# Regenerate a version's known-pack table from the extracted reference data.
knownpackgen version="26.2":
    @cargo run -p protogen -- knownpacks reference/{{ version }} {{ version }} pomme-protocol/src/data/known-packs-{{ version }}.json

# Regenerate a version's block-state table from the data-generator report.
blockgen version="26.2":
    @cargo run -p blockgen -- blocks reference/{{ version }}/generated/reports/blocks.json {{ version }} pomme-client/src/world/block/data/blocks-{{ version }}.json

# Optional JDK 25 bin override; otherwise stategen uses JAVA_HOME/bin.
jdk := ""

# TODO: Windows only (javac.exe, ';' classpath separator); make portable.

# Regenerate a version's per-state property table by running vanilla's own code
# (tools/stategen/StateDump.java) against the reference server jar, then
# compacting the dump with `blockgen state`. Uses the deobf server jar when
# one exists (pre-26.x); needs JDK 25 for 26.x class files.
stategen version="26.2":
    #!/usr/bin/env bash
    set -euo pipefail
    v="{{ version }}"
    ref="reference/$v"
    jdk="{{ jdk }}"
    if [ -z "$jdk" ]; then
        : "${JAVA_HOME:?Set JAVA_HOME or pass just jdk=/path/to/jdk/bin stategen}"
        jdk="$JAVA_HOME/bin"
    fi
    classes="$ref/server-$v.jar"
    if [ -f "$ref/server-$v-deobf.jar" ]; then classes="$ref/server-$v-deobf.jar"; fi
    if ! find "$ref/bundler" -name '*.jar' 2>/dev/null | grep -q .; then
        # This unzip build doesn't glob archive members; list them explicitly.
        unzip -Z1 "$ref/server.jar" | grep '^META-INF/libraries/.*\.jar$' \
            | xargs unzip -qn "$ref/server.jar" -d "$ref/bundler"
    fi
    libs=$(find "$ref/bundler" -name '*.jar' | tr '\n' ';')
    mkdir -p tools/stategen/out
    "$jdk/javac.exe" --release 21 -d tools/stategen/out tools/stategen/StateDump.java
    "$jdk/java.exe" -cp "$classes;${libs}tools/stategen/out" StateDump "$v" "$ref/generated/state.json"
    cargo run -p blockgen -- state "$ref/generated/state.json" pomme-client/src/world/block/data/blocks-"$v".json pomme-client/src/world/block/data/state-"$v".json
